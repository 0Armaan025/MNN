#version 440 core
layout(std430) buffer;
layout(std430) uniform;
layout(set=0, binding=0, rgba16f) writeonly restrict highp uniform image2D uOutput;

layout(set=0, binding=1) readonly buffer sourceBuffer{
    vec4 data[];
} uInBuffer;

layout(set=0, binding=2) uniform constBuffer{
    int width;
    int height;
    int channel;
    int batch;
} uConstant;

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

void main()
{
    ivec3 pos = ivec3(gl_GlobalInvocationID);
    int channelC4 = (uConstant.channel + 3) / 4;
    int batchC4 = (uConstant.batch + 3) / 4;
    int totalChannelC4 = channelC4 * uConstant.width * uConstant.height;
    if (pos.x < totalChannelC4 && pos.y < batchC4) {
        int startIndex = pos.y * 4 * totalChannelC4 + pos.x;
        vec4 v0 = uInBuffer.data[startIndex];
        vec4 v1 = vec4(0);
        vec4 v2 = vec4(0);
        vec4 v3 = vec4(0);
        if (pos.y * 4 + 1 < uConstant.batch) {
            v1 = uInBuffer.data[startIndex + 1 * totalChannelC4];
        }
        if (pos.y * 4 + 2 < uConstant.batch) {
            v2 = uInBuffer.data[startIndex + 2 * totalChannelC4];
        }
        if (pos.y * 4 + 3 < uConstant.batch) {
            v3 = uInBuffer.data[startIndex + 3 * totalChannelC4];
        }
        imageStore(uOutput, ivec2(4 * pos.x + 0, pos.y), vec4(v0.x, v1.x, v2.x, v3.x));
        imageStore(uOutput, ivec2(4 * pos.x + 1, pos.y), vec4(v0.y, v1.y, v2.y, v3.y));
        imageStore(uOutput, ivec2(4 * pos.x + 2, pos.y), vec4(v0.z, v1.z, v2.z, v3.z));
        imageStore(uOutput, ivec2(4 * pos.x + 3, pos.y), vec4(v0.w, v1.w, v2.w, v3.w));
    }
}
