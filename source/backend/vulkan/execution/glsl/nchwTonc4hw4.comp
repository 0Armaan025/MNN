#version 440 core
layout(std430) buffer;

layout(set=0, binding=0) readonly buffer sourceBuffer{
    float data[];
} uInBuffer;


layout(set=0, binding=1, std140) writeonly buffer destBuffer{
    vec4 data[];
}uOutBuffer;


layout(set=0, binding=2) uniform constBuffer{
    int width;
    int height;
    int channel;
    int batch;
} uConstant;

layout (local_size_x = 2, local_size_y = 2, local_size_z = 32) in;

void main()
{
    ivec3 pos = ivec3(gl_GlobalInvocationID);
    int channelC4 = (uConstant.channel + 3) / 4;
    int batchIndex = pos.z / channelC4;
    int zDiv4 = pos.z % channelC4;

    int lastZ = uConstant.channel / 4;
    int cIndex = uConstant.channel % 4;

    if (pos.x < uConstant.width && pos.y < uConstant.height && pos.z < channelC4 * uConstant.batch)
    {
        vec4 color = vec4(0);
        int z = zDiv4*4;
        int basicOffset = 0
            + uConstant.height*batchIndex*uConstant.channel*uConstant.width
            + z*uConstant.width*uConstant.height
            + uConstant.width*pos.y
            + pos.x;

        if(zDiv4 == lastZ)
        {
            if(cIndex == 1)
            {
                color.r = uInBuffer.data[basicOffset+0];
                color.g = 0.0;
                color.b = 0.0;
                color.a = 0.0;
            }
            else if(cIndex == 2)
            {
                color.r = uInBuffer.data[basicOffset+0];
                color.g = uInBuffer.data[basicOffset+1*uConstant.width*uConstant.height];
                color.b = 0.0;
                color.a = 0.0;
            }
            else
            {
                color.r = uInBuffer.data[basicOffset+0];
                color.g = uInBuffer.data[basicOffset+1*uConstant.width*uConstant.height];
                color.b = uInBuffer.data[basicOffset+2*uConstant.width*uConstant.height];
                color.a = 0.0;
            }
        }
        else
        {
            color.r = uInBuffer.data[basicOffset+0];
            color.g = uInBuffer.data[basicOffset+1*uConstant.width*uConstant.height];
            color.b = uInBuffer.data[basicOffset+2*uConstant.width*uConstant.height];
            color.a = uInBuffer.data[basicOffset+3*uConstant.width*uConstant.height];
        }

        uOutBuffer.data[0
            + pos.x
            + uConstant.width*pos.y
            + uConstant.width*uConstant.height*zDiv4
            + batchIndex*uConstant.width*uConstant.height*channelC4
            ] = color;
    }
}
